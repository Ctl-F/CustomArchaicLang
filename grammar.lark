?start: (proc_unit|lib_unit|test_unit)+

value: NUMBER
    | STRING
    | "true" -> true
    | "false" -> false
    | "null" -> null
    | CHAR
    // add other stuff here

static_array: "{" [value [("," value)+] ] "}"

keyval_pair: NAME ":" value
?keyval_list: keyval_pair [("," keyval_pair)+]


param: TYPE NAME
param_list: param [("," param)+] [("," VA_PARAMS)]

VA_PARAMS: "$va_args{}"
va_args: "$va_args{" expression [("," expression)+] "}"
va_arg: "$varg{" TYPE "}"

?argument: expression
?argument_list: argument [("," argument)+] [("," va_args)]

proc_unit: "proc" NAME [ "(" keyval_list ")" ] unit_body -> begin_proc

lib_unit: "lib" NAME unit_body -> begin_lib
test_unit: "test" NAME ";" -> begin_test
unit_body: "{" toplevel_stmt* "}"

struct_member: TYPE NAME ";"

struct_def: [GLOBAL] "struct" NAME "{" struct_member* "}"
struct_member_offset: "$struct" "{" [NAME "."] NAME "," NAME "}"

?toplevel_stmt: link_stmt ";"
    | static_allocation ";"
    | func_def
    | raw_c_statement
    | struct_def

link_stmt: "link" NAME [ ("," NAME)+ ] -> link
    | "c_include" STRING -> c_include

func_def: GLOBAL? "fn" NAME "(" [param_list] ")" TYPE? func_body -> function
func_body: "{" statement* "}" -> function_body
func_call: lvalue "(" argument_list? ")"

static_allocation: "static" TYPE NAME [ "=" ( macro_buffer | value | static_array ) ]
stack_allocation: "stack" TYPE NAME [ "=" ( macro_buffer | value | static_array ) ]

GLOBAL: "glob"

?macro: "$sizeof" "{" TYPE "}" -> macro_sizeof 
    | struct_member_offset

macro_buffer: "$buffer" "{" TYPE "," NUMBER "}" -> macro_buffer_typed_alloc
    | "$buffer" "{" [ NAME "." ] NAME "," NUMBER "}" -> macro_buffer_struct_alloc
    | "$buffer" "{" NUMBER "}" -> macro_buffer_bytes_alloc

static_offset: "+" (NUMBER | NAME | macro) -> static_plus
    | "-" (NUMBER | NAME | macro) -> static_minus

?lvalue: [NAME "."] NAME -> var
    | "[" [ TYPE ] [NAME "."] NAME static_offset* "]" -> deref_var
    | "[" [ TYPE ] func_call static_offset* "]" -> deref_func_call

expression: assignment

?assignment: logic_eq
    | lvalue "+=" logic_eq -> plus_eq
    | lvalue "-=" logic_eq -> minus_eq
    | lvalue "*=" logic_eq -> mul_eq
    | lvalue "/=" logic_eq -> div_eq
    | lvalue "%=" logic_eq -> mod_eq
    | lvalue "=" logic_eq -> assign

?logic_eq: logic_andor
    | logic_eq "==" logic_andor -> logic_equals
    | logic_eq "!=" logic_andor -> logic_notequals

?logic_andor: logic_cmp
    | logic_andor "&&" logic_cmp -> logic_and
    | logic_andor "||" logic_cmp -> logic_or

?logic_cmp: bin_or
    | logic_cmp ">=" bin_or -> logic_ge
    | logic_cmp "<=" bin_or -> logic_le
    | logic_cmp ">" bin_or -> logic_gt
    | logic_cmp "<" bin_or -> logic_lt

?bin_or: bin_xor
    | bin_or "|" bin_xor

?bin_xor: bin_and
    | bin_xor "^" bin_and

?bin_and: bin_shift
    | bin_and "&" bin_shift

?bin_shift: sum
    | bin_shift ">>" sum -> bin_rshift
    | bin_shift "<<" sum -> bin_lshift

?sum: product
    | sum "+" product -> add
    | sum "-" product -> sub

?product: atom
    | product "*" atom -> mul
    | product "/" atom -> div
    | product "%" atom -> mod

?atom: value 
    | macro
    | "-" atom -> neg
    | "!" atom -> not
    | "~" atom -> bin_not
    | "@" lvalue -> ref
    | func_call
    | va_arg
    | lvalue
    | atom "++" -> post_inc
    | atom "--" -> post_dec
    | "++" atom -> pre_inc
    | "--" atom -> pre_dec
    | "(" expression ")" -> group

?statement: stack_allocation ";"
    | expression ";"
    | if_statement
    | while_statement
    | do_while_statement ";"
    | for_statement
    | return_statement
    | loop_flow_stmt
    | raw_c_statement

if_statement: "if" expression "{" statement* "}" else_branch*
?else_branch: "else" if_statement -> else_if
    | else

else: "else" "{" statement* "}"


?loop_flow_stmt: "break" ";" -> break
    | "continue" ";" -> continue

return_statement: "return" expression? ";"

while_statement: "while" expression "{" (statement)* "}" [else]
do_while_statement: "do" "{" (statement)* "}" "while" expression
for_statement: "for" "(" [expression] ";" expression ";" [expression] ")" "{" (statement)* "}" [else]

INLINE_C_BODY: /\$c\{\{.*\}\}/

raw_c_statement: INLINE_C_BODY ";"

TYPE: PRIMITIVE_TYPE
    | "$struct{" [NAME "."] NAME "}" 

PRIMITIVE_TYPE: ".cstr"
    | ".i32"
    | ".i64"
    | ".i8" 
    | ".i16"
    | ".f32"
    | ".f64"
    | ".ptr"
    | ".i8_ptr"
    | ".i16_ptr"
    | ".i32_ptr"
    | ".i64_ptr"
    | ".f32_ptr"
    | ".f64_ptr"
    | ".ptr_ptr"

CHAR: /'\\?.'/

%import common.CNAME -> NAME
%import common.NUMBER -> NUMBER
%import common.WS -> WS
%import common.CPP_COMMENT -> SLINE_COMMENT
%import common.C_COMMENT   -> MLINE_COMMENT
%import common.ESCAPED_STRING -> STRING

%ignore WS
%ignore SLINE_COMMENT
%ignore MLINE_COMMENT